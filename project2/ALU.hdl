// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Not16(in=x , out=notx );
    And16(a=x , b=notx , out=zero );
    Mux16(a=x , b=zero , sel=zx , out=afterzx );
    Not16(in=afterzx , out=nafterzx );
    Mux16(a=afterzx , b=nafterzx , sel=nx , out=afternx );

    Mux16(a=y , b=zero , sel=zy , out=afterzy );
    Not16(in=afterzy , out=nafterzy );
    Mux16(a=afterzy , b=nafterzy , sel=ny , out=afterny );

    Add16(a = afternx, b = afterny, out = sum);
    And16(a = afternx, b = afterny, out = and);

    Mux16(a=and , b=sum , sel=f , out=firstout );
    Not16(in=firstout , out=notfirstout );

    Mux16(a=firstout , b=notfirstout , sel=no, out=out,  // Learned you can have multiple outputs and outputs that are only a slice of the overall output
                                               out=outinternal,
                                               out[15]=ng,
                                               out[0..7]=zero1helper,
                                               out[8..15]=zero2helper );
    Or8Way(in=zero1helper , out=z1 );
    Or8Way(in=zero2helper , out=z2 );
    Or(a=z1 , b=z2 , out=z3 );
    Not(in=z3 , out=zr );

}